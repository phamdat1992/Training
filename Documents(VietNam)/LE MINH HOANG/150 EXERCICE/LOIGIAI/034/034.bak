program Exchange;
const
  InputFile  = 'COIN.INP';
  OutputFile = 'COIN.OUT';
  maxCoin  = 20;
  maxMoney = 6666;
var
  Value: array[1..maxCoin] of Word;
  Count: array[0..maxMoney] of LongInt;
  Min: array[0..MaxMoney] of Integer;
  C: array[0..maxCoin] of Integer;
  N: Word;

procedure Enter;
var
  f: Text;
begin
  Assign(f, InputFile); Reset(f);
  Readln(f, N);
  Close(f);
end;

procedure Init;
var
  i: Integer;
begin
  for i := 1 to maxCoin do Value[i] := Sqr(i);
end;

procedure Optimize1;
var
  t: Word;
  i: Integer;
begin
  FillChar(Count, SizeOf(Count), 0);
  Count[0] := 1;
  for i := 1 to maxCoin do
    for t := 0 to N do {Tinh Count[i, t]}
      if t >= Value[i] then
        Count[t] := Count[t] + Count[t - Value[i]];
end;

procedure Optimize2;
var
  t: Word;
  i: Integer;
begin
  Min[0] := 0;
  for t := 1 to N do
    begin
      Min[t] := 255;
      for i := 1 to MaxCoin do
        if (t >= Value[i]) and (Min[t] > Min[t - Value[i]] + 1) then
          Min[t] := Min[t - Value[i]] + 1;
    end;
  FillChar(C, SizeOf(C), 0);
  t := N;
  while t <> 0 do
    begin
      for i := 1 to MaxCoin do
        if (t >= Value[i]) and (Min[t] = Min[t - Value[i]] +  1) then Break;
      Inc(C[i]);
      t := t - Value[i];
    end;
end;

procedure Result;
var
  f: Text;
  i: Integer;
begin
  Assign(f, OutputFile); Rewrite(f);
  Writeln(f, Count[N]);
  Writeln(f, Min[N]);
  for i := 1 to MaxCoin do
    if C[i] <> 0 then Writeln(f, C[i], ' ', i);
  Close(f);
end;

begin
  Enter;
  Init;
  Optimize1;
  Optimize2;
  Result;
end.
